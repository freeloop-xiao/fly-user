plugins {
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
}

group = 'com.fly'
version = '0.0.1-SNAPSHOT'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
    mavenCentral()
}

dependencies {
    implementation 'com.alibaba:fastjson:1.2.68'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'com.auth0:java-jwt:3.10.3'
    implementation 'cn.hutool:hutool-all:5.2.1'
    implementation 'mysql:mysql-connector-java:8.0.16'
    implementation 'com.alibaba:druid-spring-boot-starter:1.1.22'
    implementation 'com.baomidou:mybatis-plus-boot-starter:3.3.2'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.apache.velocity:velocity-engine-core:2.2'
    testImplementation 'com.baomidou:mybatis-plus-generator:3.3.2'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

// 将依赖包复制到lib目录
task copyJar(type: Copy) {
    // 清除现有的lib目录
    delete "$buildDir\\libs\\lib"
    from configurations.compileClasspath
    into "$buildDir\\libs\\lib"
}

// 拷贝配置文件
task copyConfigFile(type: Copy) {
    // 清除现有的配置目录
    delete "$buildDir\\libs\\config"
    from('src/main/resources/application.yaml',
            'src/main/resources/application-local.yaml',
            'src/main/resources/application-dev.yaml',
            'src/main/resources/application-test.yaml',
            'src/main/resources/application-prod.yaml')
    into 'build/libs/config'
}

// 配置bootJar进行打包
bootJar {
    // jar包名称，默认是settings.gradle下的工程名 rootProject.name = 'alog-server'
    // 排除所有的jar
    excludes = ["*.jar"]
    // lib目录的清除和复制任务
    dependsOn copyJar
    // 配置目录的清除和复制任务
    dependsOn copyConfigFile

    // 指定依赖包的路径
    manifest {
        attributes "Manifest-Version": 1.0,
                'Class-Path': configurations.compileClasspath.files.collect { "lib/$it.name" }.join(' ')
    }
}
test {
    useJUnitPlatform()
}
